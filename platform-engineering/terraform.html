<!DOCTYPE html>

<html lang="en">

<head>

<!-- Metadata -->
<meta charset="utf-8">
<meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no">
<meta name="description" content="">
<meta name="author" content="Seungmoon Rieh">
<meta name="keywords" content="">

<!-- Title and image -->
<title>Seungmoon Rieh</title>
<link href="/img/seungmoonrieh.jpg" rel="icon">

<!-- CSS -->
<link href="/css/bootstrap.min.css" rel="stylesheet">
<link href="/css/monokai-sublime.css" rel="stylesheet">
<link href="/css/site.css" rel="stylesheet">

<!-- JavaScript -->
<script src="/js/jquery.min.js"></script>
<script src="/js/bootstrap.bundle.min.js"></script>
<script src="/js/highlight.pack.js"></script>
<script type="text/javascript" src="/js/include_html.js"></script>
<script type="text/javascript" src="/js/site.js"></script>

</head>

<body>

<include src="/header.html"></include>

<div class="container">
<div class="row">
<div class="col-md-12">
<h1 class="my-4">Platform Engineering</h1>

<!-- Terraform BEGIN -->
<div class="card mb-4" id="terraform">
  <div class="card-body">
    <h2 class="card-title">Terraform</h2>
    <ul class="list-unstyled mb-0">
      <li><a href="#terraform-1">Terraform basic</a></li>
      <li><a href="#terraform-2">Terraform on GCP - Kubernetes</a></li>
      <li><a href="#terraform-3">Terraform on GCP - storage</a></li>
      <li><a href="#terraform-4">Terraform with GCP - common errors</a></li>
      <li><a href="#terraform-6">Terraform on AWS</a></li>
    </ul>
  </div>
</div>

<div class="card mb-4" id="terraform-1">
  <div class="card-body">
    <h2 class="card-title">Terraform basic</h2>
    <p class="card-text"></p>

    <ul>
      <li>Terraform "state" allows tracking changes in resources.</li>
      <li>Terraform plugins are call "providers", which let Terraform interact with cloud providers via their APIs.</li>
    </ul>

    <h3 class="card-title">Module</h3>
    <ul>
      <li>Organize, encpasulate, and re-use configuration.</li>
      <li>A modules is configuration files in a single directory.</li>
      <li>Provider must be named "terraform-&lt;provider&gt;-&lt;name&gt;"</li>
    </ul>

    <h3 class="card-title">Command</h3>
    <ul>
      <li><code>terraform init</code> - install plugins.</li>
      <li><code>terraform plan</code> - preview the change.</li>
      <li><code>terraform apply</code> - make the planned changes.</li>
      <li><code>terraform destroy</code> - tear down instances deployed.</li>
    </ul>

    <h3 class="card-title">Installation (Ubuntu)</h3>
<pre><code class="bash">sudo apt-get install -y gnupg software-properties-common

wget -O- https://apt.releases.hashicorp.com/gpg | \
gpg --dearmor | \
sudo tee /usr/share/keyrings/hashicorp-archive-keyring.gpg

echo "deb [signed-by=/usr/share/keyrings/hashicorp-archive-keyring.gpg] \
    https://apt.releases.hashicorp.com $(lsb_release -cs) main" | \
    sudo tee /etc/apt/sources.list.d/hashicorp.list

sudo apt-get install terraform

terraform -v</code></pre>

    <h3 class="card-title">Installation (CentOS) - version 0.11.13</h3>
<pre><code class="bash">yum install -y zip unzip
wget https://releases.hashicorp.com/terraform/0.9.8/terraform_0.11.13_linux_amd64.zip
unzip terraform_0.11.13_linux_amd64.zip
mv terraform /usr/local/bin/
terraform --version</pre></code>

  </div>
  <div class="card-footer text-muted">
    Reference: <a href="https://learn.hashicorp.com/collections/terraform/aws-get-started/">Get Started - AWS</a> | <a href="https://learn.hashicorp.com/tutorials/terraform/module">Modules</a>
  </div>
</div>

<div class="card mb-4" id="terraform-2">
  <div class="card-body">
    <h2 class="card-title">Terraform on GCP - Kubernetes</h2>

    <h3 class="card-title">Example: main.tf</h3>
<pre><code class="bash">provider "google" {
  credentials = "${file("../../googlecloud/credential.json")}"
  project     = "${var.project}"
}

module "vpc" {
  source                 = "../modules/global"
  var_env                = "${var.env}"
  var_company            = "${var.company}"
  var_region_name        = "${var.region_name}"
  var_ue1_public_subnet  = "${var.ue1_public_subnet}"
  var_ue1_private_subnet = "${var.ue1_private_subnet}"
}

module "ue1" {
  source                 = "../modules/ue1"
  var_env                = "${var.env}"
  var_company            = "${var.company}"
  var_region_name        = "${var.region_name}"
  var_ue1_public_subnet  = "${var.ue1_public_subnet}"
  var_ue1_private_subnet = "${var.ue1_private_subnet}"
  network_self_link      = "${module.vpc.vpc_self_link}"
  subnetwork1            = "${module.ue1.public_subnet_name}"
}</code></pre>

    <h3 class="card-title">Example: vpc.tf</h3>
<pre><code class="bash">resource "google_compute_network" "vpc" {
  name                    = "${format("%s","${var.var_company}-vpc")}"
  auto_create_subnetworks = "false"
  routing_mode            = "GLOBAL"
}

resource "google_compute_firewall" "allow-internal" {
  name    = "${var.var_company}-fw-allow-internal"
  network = "${google_compute_network.vpc.name}"
  allow {
    protocol = "icmp"
  }
  allow {
    protocol = "tcp"
    ports    = ["0-65535"]
  }
  allow {
    protocol = "udp"
    ports    = ["0-65535"]
  }
}

resource "google_compute_firewall" "allow-http" {
  name    = "${var.var_company}-fw-allow-http"
  network = "${google_compute_network.vpc.name}"
  allow {
    protocol = "tcp"
    ports    = ["80"]
  }
  target_tags = ["http"]
}

resource "google_compute_firewall" "allow-bastion" {
  name    = "${var.var_company}-fw-allow-bastion"
  network = "${google_compute_network.vpc.name}"
  allow {
    protocol = "tcp"
    ports    = ["22"]
  }
  target_tags = ["ssh"]
}</code></pre>

    <h3 class="card-title">Example: network.tf</h3>
<pre><code class="bash">resource "google_compute_subnetwork" "public_subnet" {
  name          = "${format("%s","${var.var_company}-${var.var_env}-${var.var_region_name}-pub-net")}"
  ip_cidr_range = "${var.var_ue1_public_subnet}"
  network       = "${var.network_self_link}"
  region        = "${var.var_region_name}"
}

resource "google_compute_subnetwork" "private_subnet" {
  name          = "${format("%s","${var.var_company}-${var.var_env}-${var.var_region_name}-pri-net")}"
  ip_cidr_range = "${var.var_ue1_private_subnet}"
  network       = "${var.network_self_link}"
  region        = "${var.var_region_name}"
}</code></pre>

    <h3 class="card-title">Example: instance.tf</h3>
<pre><code class="bash">resource "google_compute_instance" "vm1" {
  name          = "k8s-master"
  machine_type  = "n1-standard-2"
  zone          = "${format("%s","${var.var_region_name}-c")}"

  tags          = ["ssh","http"]

  boot_disk {
    initialize_params {
      image = "centos-7-v20180129"
    }
  }

  metadata {
    foo = "bar"
  }

  network_interface {
    subnetwork = "${google_compute_subnetwork.public_subnet.name}"

    access_config {
      // Ephemeral IP
    }
  }

  provisioner "remote-exec" {
    # You cannot open interactive session with "sudo -i". You must also run all yum commands with -y flag
    inline = [
      "sudo yum install ansible -y"
    ]

    connection {
      host        = "${self.network_interface.0.access_config.0.nat_ip}"
      type        = "ssh"
      user        = "${var.ssh_user}"
      private_key = "${file("~/.ssh/id_rsa")}"
    }
  }

  provisioner "local-exec" {
    # Environment variable can be used inside ansible playbook
    environment {
      PUBLIC_IP                 = "${self.network_interface.0.access_config.0.nat_ip}"
      HOSTNAME                  = "k8s-master"
      ANSIBLE_HOST_KEY_CHECKING = false # This is must to avoid the error "The authenticity of host can't be established"
    }

    # You must install "ansible" on the machine where terraform-ansible suites get executed
    # Add "-vvv" for verbose output
    command     = "ansible-playbook -u ${var.ssh_user} --private-key ~/.ssh/id_rsa ../ansible/k8s-master.yaml -i $PUBLIC_IP,"
  }
}</code></pre>

  </div>
  <div class="card-footer text-muted">
    Reference: <a href="https://medium.com/slalom-technology/a-complete-gcp-environment-with-terraform-c087190366f0">A complete GCP environment with Terraform</a>
  </div>
</div>

<div class="card mb-4" id="terraform-3">
  <div class="card-body">
    <h2 class="card-title">Terraform with GCP - storage</h2>

<pre><code class="bash"># Create a GCS Bucket
resource "google_storage_bucket" "my_bucket" {
  name     = var.bucket_name
  location = var.region
}

resource "google_pubsub_topic" "example" {
  name = "example-topic"
}

resource "google_pubsub_topic_iam_member" "member" {
  project = google_pubsub_topic.example.project
  topic = google_pubsub_topic.example.name
  role = "roles/viewer"
  member = "user:jane@example.com"
}

resource "google_storage_notification" "notification" {
  bucket         = google_storage_bucket.bucket.name
  payload_format = "JSON_API_V1"
  topic          = google_pubsub_topic.topic.id
  event_types    = ["OBJECT_FINALIZE"]
}</code></pre>
  </div>
  <div class="card-footer text-muted">
    Reference: <a href="https://pbhadani.com/posts/first-gcp-resource-with-terraform/">Create a Google Cloud Storage(GCS) Bucket with Terraform</a> | <a href="https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/pubsub_topic">google_pubsub_topic</a> | <a href="https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/pubsub_topic_iam">IAM policy for Cloud Pub/Sub Topic</a> | <a href="https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/storage_notification">google_storage_notification</a>
  </div>
</div>

<div class="card mb-4" id="terraform-4">
  <div class="card-body">
    <h2 class="card-title">Terraform with GCP - common errors</h2>
    <ul>
      <li><strong>"Invalid JWT: Token must be a short-lived token (60 minutes) and in a reasonable timeframe</strong> - This happens when the date and time of OS is out of sync. Run <strong>date +%T -s "10:13:13"</strong> [10: Hour (hh) 13: Minute (mm) 13: Second (ss)] to update it.</li>
    </ul>
  </div>
  <div class="card-footer text-muted">

  </div>
</div>

<div class="card mb-4" id="terraform-6">
  <div class="card-body">
    <h2 class="card-title">Terraform on AWS</h2>

    <h3 class="card-title">How to get access key</h3>
    <img class="img-fluid" class="card-img-top" src="/img/terraform/terraform1.png" alt="Card image cap">
    <img class="img-fluid" class="card-img-top" src="/img/terraform/terraform2.png" alt="Card image cap">
    <br>
    <br>

    <h3 class="card-title">In the shell, create environment variables such that</h3>
<pre><code class="bash">export AWS_ACCESS_KEY_ID=
export AWS_SECRET_ACCESS_KEY=</code></pre>

    <h3 class="card-title">main.tf</h3>
<pre><code class="terraform"># Terraform setting.
terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"  # Short hand for "registry.terraform.io/hashicorp/aws".
      version = "~> 4.16"  # If not specified, defaults to the latest version.
    }
  }

  required_version = ">= 1.2.0"
}

# Provider setting.
provider "aws" {
  region  = "us-west-2"
}

# Infrastructure setting.
resource "aws_instance" "app_server" {
  ami           = "ami-830c94e3"
  instance_type = "t2.micro"

  tags = {
    Name = var.instance_name
  }
}</code></pre>

    <h3 class="card-title">variable.tf</h3>
<pre><code class="terraform">variable "instance_name" {
  description = "Value of the Name tag for the EC2 instance"
  type        = string
  default     = "ExampleAppServerInstance"
}</code></pre>

<pre><code class="bash">terraform init
terraform fmt
terraform validate
terraform apply

# Inspect state.
terraform show

# Inspect output values.
terraform output</code></pre>

  </div>
  <div class="card-footer text-muted">
    Reference: <a href="https://learn.hashicorp.com/collections/terraform/aws-get-started">Get Started - AWS</a>
  </div>
</div>

<div class="card mb-4" id="terraform-">
  <div class="card-body">
    <h2 class="card-title"></h2>
    <p class="card-text"></p>

    <h3 class="card-title">main.tf</h3>
<pre><code class="terraform"># Configure AWS provider.
provider "aws" {
  region = "us-west-2"

  default_tags {
    tags = {
      hashicorp-learn = "module-use"
    }
  }
}

# Provision networking resources. (VPC, subnets, internet, NAT gateways)
module "vpc" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "3.14.0"

  name = var.vpc_name
  cidr = var.vpc_cidr

  azs             = var.vpc_azs
  private_subnets = var.vpc_private_subnets
  public_subnets  = var.vpc_public_subnets

  enable_nat_gateway = var.vpc_enable_nat_gateway

  tags = var.vpc_tags
}

# Provision instances.
module "ec2_instances" {
  source  = "terraform-aws-modules/ec2-instance/aws"
  version = "3.5.0"
  count   = 2

  name = "my-ec2-cluster"

  ami                    = "ami-0c5204531f799e0c6"
  instance_type          = "t2.micro"
  vpc_security_group_ids = [module.vpc.default_security_group_id]
  subnet_id              = module.vpc.public_subnets[0]

  tags = {
    Terraform   = "true"
    Environment = "dev"
  }
}</code></pre>

    <h3 class="card-title">variable.tf</h3>
<pre><code class="terraform">variable "vpc_name" {
  description = "Name of VPC"
  type        = string
  default     = "example-vpc"
}

variable "vpc_cidr" {
  description = "CIDR block for VPC"
  type        = string
  default     = "10.0.0.0/16"
}

variable "vpc_azs" {
  description = "Availability zones for VPC"
  type        = list(string)
  default     = ["us-west-2a", "us-west-2b", "us-west-2c"]
}

variable "vpc_private_subnets" {
  description = "Private subnets for VPC"
  type        = list(string)
  default     = ["10.0.1.0/24", "10.0.2.0/24"]
}

variable "vpc_public_subnets" {
  description = "Public subnets for VPC"
  type        = list(string)
  default     = ["10.0.101.0/24", "10.0.102.0/24"]
}

variable "vpc_enable_nat_gateway" {
  description = "Enable NAT gateway for VPC"
  type        = bool
  default     = true
}

variable "vpc_tags" {
  description = "Tags to apply to resources created by VPC module"
  type        = map(string)
  default = {
    Terraform   = "true"
    Environment = "dev"
  }
}</code></pre>

    <h3 class="card-title">outputs.tf</h3>
<pre><code class="terraform">output "vpc_public_subnets" {
  description = "IDs of the VPC's public subnets"
  value       = module.vpc.public_subnets
}

output "ec2_instance_public_ips" {
  description = "Public IP addresses of EC2 instances"
  value       = module.ec2_instances[*].public_ip
}</code></pre>

  </div>
  <div class="card-footer text-muted">
    <a href="https://learn.hashicorp.com/tutorials/terraform/module-use?in=terraform/modules">Use Modules from the Registry</a>
  </div>
</div>

<div class="card mb-4" id="terraform-">
  <div class="card-body">
    <h2 class="card-title"></h2>
    <p class="card-text"></p>

    <h3 class="card-title">main.tf</h3>
<pre><code class="terraform"># Create a public S3 bucket which hosts a website.
# Assuming parent modules has "provider" block, there is no "provider" block in this configuration.
resource "aws_s3_bucket" "s3_bucket" {
  bucket = var.bucket_name

  tags = var.tags
}

resource "aws_s3_bucket_website_configuration" "s3_bucket" {
  bucket = aws_s3_bucket.s3_bucket.id

  index_document {
    suffix = "index.html"
  }

  error_document {
    key = "error.html"
  }
}

resource "aws_s3_bucket_acl" "s3_bucket" {
  bucket = aws_s3_bucket.s3_bucket.id

  acl = "public-read"
}

resource "aws_s3_bucket_policy" "s3_bucket" {
  bucket = aws_s3_bucket.s3_bucket.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid       = "PublicReadGetObject"
        Effect    = "Allow"
        Principal = "*"
        Action    = "s3:GetObject"
        Resource = [
          aws_s3_bucket.s3_bucket.arn,
          "${aws_s3_bucket.s3_bucket.arn}/*",
        ]
      },
    ]
  })
}</code></pre>

    <h3 class="card-title">variable.tf</h3>
<pre><code class="terraform">variable "bucket_name" {
  description = "Name of the s3 bucket. Must be unique."
  type        = string
}

variable "tags" {
  description = "Tags to set on the bucket."
  type        = map(string)
  default     = {}
}
</code></pre>

    <h3 class="card-title">outputs.tf</h3>
<pre><code class="terraform">output "arn" {
  description = "ARN of the bucket"
  value       = aws_s3_bucket.s3_bucket.arn
}

output "name" {
  description = "Name (id) of the bucket"
  value       = aws_s3_bucket.s3_bucket.id
}

output "domain" {
  description = "Domain name of the bucket"
  value       = aws_s3_bucket_website_configuration.s3_bucket.website_domain
}</code></pre>

    <h3 class="card-title">main.tf (at the root)</h3>
<pre><code class="terraform"># Add this block to an existing "main.tf" at the root.
module "website_s3_bucket" {
  source = "./modules/aws-s3-static-website-bucket"

  bucket_name = "<UNIQUE BUCKET NAME>"

  tags = {
    Terraform   = "true"
    Environment = "dev"
  }
}</code></pre>

    <h3 class="card-title">outputs.tf (at the root)</h3>
<pre><code class="terraform"># Add this block to an existing "outputs.tf" at the root.
output "vpc_public_subnets" {
  description = "IDs of the VPC's public subnets"
  value       = module.vpc.public_subnets
}

output "ec2_instance_public_ips" {
  description = "Public IP addresses of EC2 instances"
  value       = module.ec2_instances.public_ip
}

output "website_bucket_arn" {
  description = "ARN of the bucket"
  value       = module.website_s3_bucket.arn
}

output "website_bucket_name" {
  description = "Name (id) of the bucket"
  value       = module.website_s3_bucket.name
}

output "website_bucket_domain" {
  description = "Domain name of the bucket"
  value       = module.website_s3_bucket.domain
}</code></pre>

    <pre><code class="bash">terraform init
terraform apply

# Upload files to S3 bucket.
# Website can be accessed at https://&lt;bucket_name&lt;.s3-us-west-2.amazonaws.com/index.html
aws s3 cp modules/aws-s3-static-website-bucket/www/ s3://$(terraform output -raw website_bucket_name)/ --recursive

# Clean up website and infrastructure.
aws s3 rm s3://$(terraform output -raw website_bucket_name)/ --recursive
terraform destroy</code></pre>

  </div>
  <div class="card-footer text-muted">
    <a href="https://learn.hashicorp.com/tutorials/terraform/module-create?in=terraform/modules">Build and Use a Local Module</a>
  </div>
</div>

<div class="card mb-4" id="terraform-">
  <div class="card-body">
    <h2 class="card-title"></h2>
    <p class="card-text"></p>

    <h3 class="card-title">main.tf</h3>
<pre><code class="terraform">provider "aws" {
  region = "us-east-2"

  default_tags {
    tags = {
      hashicorp-learn = "aws-asg"
    }
  }
}

data "aws_availability_zones" "available" {
  state = "available"
}

# Create VPC with public subnets. Other resources depend on outputs of this module.
module "vpc" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "2.77.0"

  name = "main-vpc"
  cidr = "10.0.0.0/16"

  azs                  = data.aws_availability_zones.available.names
  public_subnets       = ["10.0.4.0/24", "10.0.5.0/24", "10.0.6.0/24"]
  enable_dns_hostnames = true
  enable_dns_support   = true
}

data "aws_ami" "amazon-linux" {
  most_recent = true
  owners      = ["amazon"]

  filter {
    name   = "name"
    values = ["amzn-ami-hvm-*-x86_64-ebs"]
  }
}

# EC2 instance configuration that ASG will use.
resource "aws_launch_configuration" "terramino" {
  name_prefix     = "learn-terraform-aws-asg-"
  image_id        = data.aws_ami.amazon-linux.id
  instance_type   = "t2.micro"
  user_data       = file("user-data.sh")  # Each instance will run this script at runtime.
  security_groups = [aws_security_group.terramino_instance.id]

  lifecycle {
    create_before_destroy = true
  }
}

# ASG.
resource "aws_autoscaling_group" "terramino" {
  name                 = "terramino"
  min_size             = 1
  max_size             = 3
  desired_capacity     = 1
  launch_configuration = aws_launch_configuration.terramino.name
  vpc_zone_identifier  = module.vpc.public_subnets

  tag {
    key                 = "Name"
    value               = "HashiCorp Learn ASG - Terramino"
    propagate_at_launch = true
  }

  # Ignore changes in "desired_capaciy"
  # When the instances are scaled manually, "terraform plan" will undesirably scale down the
  # instances based on the initial configuration.
  lifecycle {
    ignore_changes = [desired_capacity, target_group_arns]
  }
}

# LB.
resource "aws_lb" "terramino" {
  name               = "learn-asg-terramino-lb"
  internal           = false
  load_balancer_type = "application"
  security_groups    = [aws_security_group.terramino_lb.id]
  subnets            = module.vpc.public_subnets
}

# How to handle HTTP requests on port 80.
resource "aws_lb_listener" "terramino" {
  load_balancer_arn = aws_lb.terramino.arn
  port              = "80"
  protocol          = "HTTP"

  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.terramino.arn
  }
}

# Collection of instances LB sends traffic to.
resource "aws_lb_target_group" "terramino" {
  name     = "learn-asg-terramino"
  port     = 80
  protocol = "HTTP"
  vpc_id   = module.vpc.vpc_id
}

# Link ASG to target group.
resource "aws_autoscaling_attachment" "terramino" {
  autoscaling_group_name = aws_autoscaling_group.terramino.id
  alb_target_group_arn   = aws_lb_target_group.terramino.arn
}

# Security group for ASG.
resource "aws_security_group" "terramino_instance" {
  name = "learn-asg-terramino-instance"
  ingress {
    from_port       = 80
    to_port         = 80
    protocol        = "tcp"
    security_groups = [aws_security_group.terramino_lb.id]  # Only accept requests forwarded by LB.
  }

  egress {
    from_port       = 0
    to_port         = 0
    protocol        = "-1"
    security_groups = [aws_security_group.terramino_lb.id]
  }

  vpc_id = module.vpc.vpc_id
}

# Security group for LB.
resource "aws_security_group" "terramino_lb" {
  name = "learn-asg-terramino-lb"
  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  vpc_id = module.vpc.vpc_id
}</code></pre>

    <h3 class="card-title">outputs.tf</h3>
<pre><code class="terraform">output "lb_endpoint" {
  value = "https://${aws_lb.terramino.dns_name}"
}

output "application_endpoint" {
  value = "https://${aws_lb.terramino.dns_name}/index.php"
}

output "asg_name" {
  value = aws_autoscaling_group.terramino.name
}</code></pre>

<pre><code class="bash">terraform init
terraform apply

# Get ID of EC2 instance.
curl $(terraform output -raw lb_endpoint)

# Scale the instances.
aws autoscaling set-desired-capacity --auto-scaling-group-name $(terraform output -raw asg_name) --desired-capacity 2

# Make requests to check if instances are scaled.
for i in `seq 1 5`; do curl $(terraform output -raw lb_endpoint); echo;  done

# Clean up infrastructure.
terraform destroy</code></pre>

  </div>
  <div class="card-footer text-muted">
    Reference: <a href="https://learn.hashicorp.com/tutorials/terraform/aws-asg?in=terraform/aws">Manage AWS Auto Scaling Groups</a>
  </div>
</div>

<div class="card mb-4" id="terraform-">
  <div class="card-body">
    <h2 class="card-title"></h2>
    <p class="card-text"></p>

    <h3 class="card-title">main.tf</h3>
<pre><code class="terraform">provider "aws" {
  region = var.region
}

data "aws_availability_zones" "available" {}

# VPC and subnets.
module "vpc" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "2.77.0"

  name                 = "education"
  cidr                 = "10.0.0.0/16"
  azs                  = data.aws_availability_zones.available.names
  public_subnets       = ["10.0.4.0/24", "10.0.5.0/24", "10.0.6.0/24"]  # Should avoid public subnets in production.
  enable_dns_hostnames = true
  enable_dns_support   = true
}

# Define subnets that RDS instance can be provisioned in.
# This is an optional block.
resource "aws_db_subnet_group" "education" {
  name       = "education"
  subnet_ids = module.vpc.public_subnets

  tags = {
    Name = "Education"
  }
}

resource "aws_security_group" "rds" {
  name   = "education_rds"
  vpc_id = module.vpc.vpc_id

  ingress {
    from_port   = 5432
    to_port     = 5432
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 5432
    to_port     = 5432
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "education_rds"
  }
}

resource "aws_db_parameter_group" "education" {
  name   = "education"
  family = "postgres13"  # Must correspond to RDS engine version.

  parameter {
    name  = "log_connections"
    value = "1"
  }
}

resource "aws_db_instance" "education" {
  identifier             = "education"
  instance_class         = "db.t3.micro"
  allocated_storage      = 5
  engine                 = "postgres"
  engine_version         = "13.1"
  username               = "edu"
  password               = var.db_password
  db_subnet_group_name   = aws_db_subnet_group.education.name  # Optional. If not specified, RSD is provisioned in default VPC.
  vpc_security_group_ids = [aws_security_group.rds.id]
  parameter_group_name   = aws_db_parameter_group.education.name
  publicly_accessible    = true  # Should be "false" in production.
  skip_final_snapshot    = true
}</code></pre>

    <h3 class="card-title">variables.tf</h3>
<pre><code class="terraform">variable "region" {
  default     = "us-east-2"
  description = "AWS region"
}

variable "db_password" {
  description = "RDS root user password"
  sensitive   = true
}</code></pre>

    <h3 class="card-title">outputs.tf</h3>
<pre><code class="terraform">output "rds_hostname" {
  description = "RDS instance hostname"
  value       = aws_db_instance.education.address
  sensitive   = true
}

output "rds_port" {
  description = "RDS instance port"
  value       = aws_db_instance.education.port
  sensitive   = true
}

output "rds_username" {
  description = "RDS instance root username"
  value       = aws_db_instance.education.username
  sensitive   = true
}</code></pre>

<pre><code class="bash">export TF_VAR_db_password="hashicorp"
terraform init
terraform apply

# Verify configuration. Enter "hashicorp" when prompted.
psql -h $(terraform output -raw rds_hostname) -p $(terraform output -raw rds_port) -U $(terraform output -raw rds_username) postgres</code></pre>

    <h3 class="card-title">main.tf (existing)</h3>
<pre><code class="terraform"># Create read replica such that data is asynchronously copied to the replica to serve read request. (To reduce load on primary DB) Add this to "main.tf".
resource "aws_db_instance" "education_replica" {
   name                   = "education-replica"
   identifier             = "education-replica"
   replicate_source_db    = aws_db_instance.education.identifier
   instance_class         = "db.t3.micro"
   apply_immediately      = true
   publicly_accessible    = true
   skip_final_snapshot    = true
   vpc_security_group_ids = [aws_security_group.rds.id]
   parameter_group_name   = aws_db_parameter_group.education.name
}

# Also, add "backup_retention_period = 1" to "aws_db_instance" in "main.tf".

# Also, add this block to "outputs.tf".
output "rds_replica_connection_parameters" {
  description = "RDS replica instance connection parameters"
  value       = "-h ${aws_db_instance.education_replica.address} -p ${aws_db_instance.education_replica.port} -U ${aws_db_instance.education_replica.username} postgres"
}
</code></pre>

<pre><code class="bash">terraform apply

# Use new endpoint to connect to read replica. Enter "hashicorp" when prompted.
psql $(terraform output -raw rds_replica_connection_parameters)

# Clean up infrastructure.
terraform destroy</code></pre>

  </div>
  <div class="card-footer text-muted">
    Reference: <a href="https://learn.hashicorp.com/tutorials/terraform/aws-rds?in=terraform/aws">Manage AWS RDS Instances</a>
  </div>
</div>

<div class="card mb-4" id="terraform-">
  <div class="card-body">
    <h2 class="card-title"></h2>
    <p class="card-text"></p>

    <h3 class="card-title"></h3>
<pre><code class="terraform"># Add this block to "main.tf"
data "aws_default_tags" "current" {}

# Add this field to resource "aws_autoscaling_group" in "main.tf"
dynamic "tag" {
  for_each = data.aws_default_tags.current.tags
  content {
    key                 = tag.key
    value               = tag.value
    propagate_at_launch = true
  }
}</code></pre>

<pre><code class="bash">terraform apply -replace aws_autoscaling_group.example

# Check tags of ASG.
aws ec2 describe-instances --region us-east-2 --filter Name=tag:aws:autoscaling:groupName,Values=$(terraform output asg_id) | jq -r '.Reservations[].Instances[].Tags'</code></pre>

    <h3 class="card-title">Override default tag</h3>
<pre><code class="terraform"># Add this field to resource "aws_instance" in "main.tf"
tags = {
  Service   = "Custom"
  ManagedBy = "Resource"
}</code></pre>

<pre><code class="bash">terraform apply

# Check tags of ASG.
aws ec2 describe-tags --region us-east-2 --filters "Name=resource-id,Values=$(terraform output instance_id)"

# Clean up infrastructure.
terraform destroy</code></pre>

  </div>
  <div class="card-footer text-muted">
    Reference: <a href="https://learn.hashicorp.com/tutorials/terraform/aws-default-tags?in=terraform/aws">Configure Default Tags for AWS Resources</a>
  </div>
</div>

<div class="card mb-4" id="terraform-">
  <div class="card-body">
    <h2 class="card-title"></h2>
    <p class="card-text"></p>

    <h3 class="card-title">main.tf</h3>
<pre><code class="terraform">provider "aws" {
  region = var.region

  default_tags {
    tags = {
      Hashicorp-Learn = "aws-iam-policy"
    }
  }

}

resource "random_pet" "pet_name" {
  length    = 3
  separator = "-"
}

resource "aws_iam_user" "new_user" {
  name = "new_user"
}

resource "aws_s3_bucket" "bucket" {
  bucket = "${random_pet.pet_name.id}-bucket"

  tags = {
    Name        = "My bucket"
    Environment = "Dev"
  }
}

resource "aws_s3_bucket_acl" "bucket" {
  bucket = aws_s3_bucket.bucket.id

  acl = "private"
}

# Policy definition.
resource "aws_iam_policy" "policy" {
  name        = "${random_pet.pet_name.id}-policy"
  description = "My test policy"
  policy = data.aws_iam_policy_document.example.json
}

# Create JSON of IAM policy document.
data "aws_iam_policy_document" "example" {
  statement {
    actions   = ["s3:ListAllMyBuckets"]
    resources = ["arn:aws:s3:::*"]
    effect = "Allow"
  }
  statement {
    actions   = ["s3:*"]
    resources = [aws_s3_bucket.bucket.arn]
    effect = "Allow"
  }
}

# Apply policy to users.
resource "aws_iam_user_policy_attachment" "attachment" {
  user       = aws_iam_user.new_user.name
  policy_arn = aws_iam_policy.policy.arn
}</code></pre>

    <h3 class="card-title">outputs.tf</h3>
<pre><code class="terraform">output "rendered_policy" {
  value = data.aws_iam_policy_document.example.json
}</pre></code>

<pre><code class="bash">terraform init
terraform apply
terraform destroy</code></pre>

  </div>
  <div class="card-footer text-muted">
    Reference: <a href="https://learn.hashicorp.com/tutorials/terraform/aws-iam-policy?in=terraform/aws">Create IAM Policies</a>
  </div>
</div>

<div class="card mb-4" id="terraform-">
  <div class="card-body">
    <h2 class="card-title"></h2>
    <p class="card-text"></p>

    <h3 class="card-title">hello-world/hello.js</h3>
<pre><code class="terraform">module.exports.handler = async (event) => {
  console.log('Event: ', event);
  let responseMessage = 'Hello, World!';

  return {
    statusCode: 200,
    headers: {
      'Content-Type': 'application/json',
    },
    body: JSON.stringify({
      message: responseMessage,
    }),
  }
}</code></pre>

    <h3 class="card-title">main.tf</h3>
<pre><code class="terraform">terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 4.0.0"
    }
    random = {
      source  = "hashicorp/random"
      version = "~> 3.1.0"
    }
    archive = {
      source  = "hashicorp/archive"
      version = "~> 2.2.0"
    }
  }

  required_version = "~> 1.0"
}

provider "aws" {
  region = var.aws_region
}

resource "random_pet" "lambda_bucket_name" {
  prefix = "learn-terraform-functions"
  length = 4
}

resource "aws_s3_bucket" "lambda_bucket" {
  bucket = random_pet.lambda_bucket_name.id
}

resource "aws_s3_bucket_acl" "bucket_acl" {
  bucket = aws_s3_bucket.lambda_bucket.id
  acl    = "private"
}

# Create a zip archive.
data "archive_file" "lambda_hello_world" {
  type = "zip"

  source_dir  = "${path.module}/hello-world"
  output_path = "${path.module}/hello-world.zip"
}

# Upload the archive to S3.
resource "aws_s3_object" "lambda_hello_world" {
  bucket = aws_s3_bucket.lambda_bucket.id

  key    = "hello-world.zip"
  source = data.archive_file.lambda_hello_world.output_path

  etag = filemd5(data.archive_file.lambda_hello_world.output_path)
}

# Configure Lambda to use bucket object.
resource "aws_lambda_function" "hello_world" {
  function_name = "HelloWorld"

  s3_bucket = aws_s3_bucket.lambda_bucket.id
  s3_key    = aws_s3_object.lambda_hello_world.key

  runtime = "nodejs12.x"
  handler = "hello.handler"

  source_code_hash = data.archive_file.lambda_hello_world.output_base64sha256

  role = aws_iam_role.lambda_exec.arn
}

# Store log messages from Lambda function.
resource "aws_cloudwatch_log_group" "hello_world" {
  name = "/aws/lambda/${aws_lambda_function.hello_world.function_name}"

  retention_in_days = 30
}

# Allow Lambda to access resources.
resource "aws_iam_role" "lambda_exec" {
  name = "serverless_lambda"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Action = "sts:AssumeRole"
      Effect = "Allow"
      Sid    = ""
      Principal = {
        Service = "lambda.amazonaws.com"
      }
      }
    ]
  })
}

# Attach policy to IAM.
resource "aws_iam_role_policy_attachment" "lambda_policy" {
  role       = aws_iam_role.lambda_exec.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSLambdaBasicExecutionRole"
}
</code></pre>

    <h3 class="card-title">outputs.tf</h3>
<pre><code class="terraform">output "lambda_bucket_name" {
  description = "Name of the S3 bucket used to store function code."

  value = aws_s3_bucket.lambda_bucket.id
}

output "function_name" {
  description = "Name of the Lambda function."

  value = aws_lambda_function.hello_world.function_name
}</code></pre>

    <h3 class="card-title">variables.tf</h3>
<pre><code class="terraform">variable "aws_region" {
  description = "AWS region for all resources."

  type    = string
  default = "us-east-1"
}</code></pre>

    <h3 class="card-title">main.tf (existing)</h3>
<pre><code class="terraform"># API Gateway is managed service for HTTP. Add these block to "main.tf".

# Define API Gateway name.
resource "aws_apigatewayv2_api" "lambda" {
  name          = "serverless_lambda_gw"
  protocol_type = "HTTP"
}

# Set up application stage. (Ex. test, staging, prod)
resource "aws_apigatewayv2_stage" "lambda" {
  api_id = aws_apigatewayv2_api.lambda.id

  name        = "serverless_lambda_stage"
  auto_deploy = true

  access_log_settings {
    destination_arn = aws_cloudwatch_log_group.api_gw.arn

    format = jsonencode({
      requestId               = "$context.requestId"
      sourceIp                = "$context.identity.sourceIp"
      requestTime             = "$context.requestTime"
      protocol                = "$context.protocol"
      httpMethod              = "$context.httpMethod"
      resourcePath            = "$context.resourcePath"
      routeKey                = "$context.routeKey"
      status                  = "$context.status"
      responseLength          = "$context.responseLength"
      integrationErrorMessage = "$context.integrationErrorMessage"
      }
    )
  }
}

# Configure API Gateway to use Lambda function.
resource "aws_apigatewayv2_integration" "hello_world" {
  api_id = aws_apigatewayv2_api.lambda.id

  integration_uri    = aws_lambda_function.hello_world.invoke_arn
  integration_type   = "AWS_PROXY"
  integration_method = "POST"
}

# Map HTTP requests to Lambda function.
resource "aws_apigatewayv2_route" "hello_world" {
  api_id = aws_apigatewayv2_api.lambda.id

  route_key = "GET /hello"
  target    = "integrations/${aws_apigatewayv2_integration.hello_world.id}"
}

# Store access logs for API Gateway.
resource "aws_cloudwatch_log_group" "api_gw" {
  name = "/aws/api_gw/${aws_apigatewayv2_api.lambda.name}"

  retention_in_days = 30
}

# Grant API Gateway permission to use Lambda function.
resource "aws_lambda_permission" "api_gw" {
  statement_id  = "AllowExecutionFromAPIGateway"
  action        = "lambda:InvokeFunction"
  function_name = aws_lambda_function.hello_world.function_name
  principal     = "apigateway.amazonaws.com"

  source_arn = "${aws_apigatewayv2_api.lambda.execution_arn}/*/*"
}</code></pre>

    <h3 class="card-title">outputs.tf (existing)</h3>
<pre><code class="terraform"># Add this block to "outputs.tf".

output "base_url" {
  description = "Base URL for API Gateway stage."

  value = aws_apigatewayv2_stage.lambda.invoke_url
}</code></pre>

<pre><code class="bash">terraform apply

# Send request to API Gateway. (This invokes Lambda function)
curl "$(terraform output -raw base_url)/hello"

# Clean up infrastructure.
terraform destroy</code></pre>

  </div>
  <div class="card-footer text-muted">
    Reference: <a href="https://learn.hashicorp.com/tutorials/terraform/lambda-api-gateway?in=terraform/aws">Deploy Serverless Applications with AWS Lambda and API Gateway</a>
  </div>
</div>

<div class="card mb-4" id="terraform-">
  <div class="card-body">
    <h2 class="card-title"></h2>
    <p class="card-text"></p>

    <h3 class="card-title">main.tf</h3>
<pre><code class="terraform">provider "kubernetes" {
  host                   = module.eks.cluster_endpoint
  cluster_ca_certificate = base64decode(module.eks.cluster_certificate_authority_data)
}

provider "aws" {
  region = var.region
}

data "aws_availability_zones" "available" {}

locals {
  cluster_name = "education-eks-${random_string.suffix.result}"
}

resource "random_string" "suffix" {
  length  = 8
  special = false
}</code></pre>

    <h3 class="card-title">version.tf</h3>
<pre><code class="terraform">terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 4.15.0"
    }

    random = {
      source  = "hashicorp/random"
      version = "3.1.0"
    }
  }

  required_version = "~> 1.2.0"
}</code></pre>

    <h3 class="card-title">vpc.tf</h3>
<pre><code class="terraform">module "vpc" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "3.14.2"

  name = "education-vpc"

  cidr = "10.0.0.0/16"
  azs  = slice(data.aws_availability_zones.available.names, 0, 3)

  private_subnets = ["10.0.1.0/24", "10.0.2.0/24", "10.0.3.0/24"]
  public_subnets  = ["10.0.4.0/24", "10.0.5.0/24", "10.0.6.0/24"]

  enable_nat_gateway   = true
  single_nat_gateway   = true
  enable_dns_hostnames = true

  public_subnet_tags = {
    "kubernetes.io/cluster/${local.cluster_name}" = "shared"
    "kubernetes.io/role/elb"                      = 1
  }

  private_subnet_tags = {
    "kubernetes.io/cluster/${local.cluster_name}" = "shared"
    "kubernetes.io/role/internal-elb"             = 1
  }
}</code></pre>

    <h3 class="card-title">eks-cluster.tf</h3>
<pre><code class="terraform">module "eks" {
  source  = "terraform-aws-modules/eks/aws"
  version = "18.26.6"

  cluster_name    = local.cluster_name
  cluster_version = "1.22"

  vpc_id     = module.vpc.vpc_id
  subnet_ids = module.vpc.private_subnets

  eks_managed_node_group_defaults = {
    ami_type = "AL2_x86_64"

    attach_cluster_primary_security_group = true

    # Disabling and using externally provided security groups
    create_security_group = false
  }

  eks_managed_node_groups = {
    one = {
      name = "node-group-1"

      instance_types = ["t3.small"]

      min_size     = 1
      max_size     = 3
      desired_size = 2

      pre_bootstrap_user_data = <<-EOT
      echo 'foo bar'
      EOT

      vpc_security_group_ids = [
        aws_security_group.node_group_one.id
      ]
    }

    two = {
      name = "node-group-2"

      instance_types = ["t3.medium"]

      min_size     = 1
      max_size     = 2
      desired_size = 1

      pre_bootstrap_user_data = <<-EOT
      echo 'foo bar'
      EOT

      vpc_security_group_ids = [
        aws_security_group.node_group_two.id
      ]
    }
  }
}</code></pre>

    <h3 class="card-title">outputs.tf</h3>
<pre><code class="terraform">output "cluster_id" {
  description = "EKS cluster ID"
  value       = module.eks.cluster_id
}

output "cluster_endpoint" {
  description = "Endpoint for EKS control plane"
  value       = module.eks.cluster_endpoint
}

output "cluster_security_group_id" {
  description = "Security group ids attached to the cluster control plane"
  value       = module.eks.cluster_security_group_id
}

output "region" {
  description = "AWS region"
  value       = var.region
}

output "cluster_name" {
  description = "Kubernetes Cluster Name"
  value       = local.cluster_name
}
</code></pre>

    <h3 class="card-title">variables.tf</h3>
<pre><code class="terraform">variable "region" {
  description = "AWS region"
  type        = string
  default     = "us-east-2"
}</code></pre>

<pre><code class="bash">terraform init
terraform apply

# Configure kubectl.
aws eks --region $(terraform output -raw region) update-kubeconfig --name $(terraform output -raw cluster_name)

kubectl cluster-info
kubectl get nodes

# Clean up infrastructure.
terraform destroy</code></pre>

  </div>
  <div class="card-footer text-muted">
    Reference: <a href="https://learn.hashicorp.com/tutorials/terraform/eks?in=terraform/aws">Provision an EKS Cluster (AWS)</a>
  </div>
</div>

<div class="card mb-4" id="terraform-">
  <div class="card-body">
    <h2 class="card-title"></h2>
    <p class="card-text"></p>

    <h3 class="card-title">main.tf</h3>
<pre><code class="terraform">provider "aws" {
  region = var.region
}

data "aws_availability_zones" "available" {
  state = "available"
}

module "vpc" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "2.64.0"

  name = "main-vpc"
  cidr = var.vpc_cidr_block

  azs             = data.aws_availability_zones.available.names
  private_subnets = slice(var.private_subnet_cidr_blocks, 0, var.private_subnet_count)
  public_subnets  = slice(var.public_subnet_cidr_blocks, 0, var.public_subnet_count)

  enable_nat_gateway = false
  enable_vpn_gateway = var.enable_vpn_gateway
}

module "app_security_group" {
  source  = "terraform-aws-modules/security-group/aws//modules/web"
  version = "3.17.0"

  name        = "web-sg"
  description = "Security group for web-servers with HTTP ports open within VPC"
  vpc_id      = module.vpc.vpc_id

  #   ingress_cidr_blocks = module.vpc.public_subnets_cidr_blocks
  ingress_cidr_blocks = ["0.0.0.0/0"]
}

module "lb_security_group" {
  source  = "terraform-aws-modules/security-group/aws//modules/web"
  version = "3.17.0"

  name        = "lb-sg"
  description = "Security group for load balancer with HTTP ports open within VPC"
  vpc_id      = module.vpc.vpc_id

  ingress_cidr_blocks = ["0.0.0.0/0"]
}

data "aws_ami" "amazon_linux" {
  most_recent = true
  owners      = ["amazon"]

  filter {
    name   = "name"
    values = ["amzn2-ami-hvm-*-x86_64-gp2"]
  }
}

resource "random_pet" "app" {
  length    = 2
  separator = "-"
}

# ALB.
resource "aws_lb" "app" {
  name               = "main-app-${random_pet.app.id}-lb"
  internal           = false
  load_balancer_type = "application"
  subnets            = module.vpc.public_subnets
  security_groups    = [module.lb_security_group.this_security_group_id]
}

resource "aws_lb_listener" "app" {
  load_balancer_arn = aws_lb.app.arn
  port              = "80"
  protocol          = "HTTP"

  default_action {
    type             = "forward"
    forward {
      target_group {
        arn    = aws_lb_target_group.blue.arn
        weight = lookup(local.traffic_dist_map[var.traffic_distribution], "blue", 100)
      }

      target_group {
        arn    = aws_lb_target_group.green.arn
        weight = lookup(local.traffic_dist_map[var.traffic_distribution], "green", 0)
      }

      stickiness {
        enabled  = false
        duration = 1
      }
    }
  }
}</code></pre>

    <h3 class="card-title">blue.tf</h3>
<pre><code class="terraform">resource "aws_instance" "blue" {
  count = var.enable_blue_env ? var.blue_instance_count : 0

  ami                    = data.aws_ami.amazon_linux.id
  instance_type          = "t2.micro"
  subnet_id              = module.vpc.public_subnets[count.index % length(module.vpc.public_subnets)]
  vpc_security_group_ids = [module.app_security_group.this_security_group_id]
  user_data = templatefile("${path.module}/init-script.sh", {
    file_content = "version 1.0 - #${count.index}"
  })

  tags = {
    Name = "version-1.0-${count.index}"
  }
}

resource "aws_lb_target_group" "blue" {
  name     = "blue-tg-${random_pet.app.id}-lb"
  port     = 80
  protocol = "HTTP"
  vpc_id   = module.vpc.vpc_id

  health_check {
    port     = 80
    protocol = "HTTP"
    timeout  = 5
    interval = 10
  }
}

resource "aws_lb_target_group_attachment" "blue" {
  count            = length(aws_instance.blue)
  target_group_arn = aws_lb_target_group.blue.arn
  target_id        = aws_instance.blue[count.index].id
  port             = 80
}</code></pre>

    <h3 class="card-title">green.tf</h3>
<pre><code class="terraform">resource "aws_instance" "green" {
  count = var.enable_green_env ? var.green_instance_count : 0

  ami                    = data.aws_ami.amazon_linux.id
  instance_type          = "t2.micro"
  subnet_id              = module.vpc.public_subnets[count.index % length(module.vpc.public_subnets)]
  vpc_security_group_ids = [module.app_security_group.this_security_group_id]
  user_data = templatefile("${path.module}/init-script.sh.tftpl", {
    file_content = "version 1.1 - #${count.index}"
  })

  tags = {
    Name = "green-${count.index}"
  }
}

resource "aws_lb_target_group" "green" {
  name     = "green-tg-${random_pet.app.id}-lb"
  port     = 80
  protocol = "HTTP"
  vpc_id   = module.vpc.vpc_id

  health_check {
    port     = 80
    protocol = "HTTP"
    timeout  = 5
    interval = 10
  }
}

resource "aws_lb_target_group_attachment" "green" {
  count            = length(aws_instance.green)
  target_group_arn = aws_lb_target_group.green.arn
  target_id        = aws_instance.green[count.index].id
  port             = 80
}</code></pre>

    <h3 class="card-title">variables.tf</h3>
<pre><code class="terraform">output "lb_dns_name" {
  value = aws_lb.app.dns_name
}

variable "enable_green_env" {
  description = "Enable green environment"
  type        = bool
  default     = true
}

variable "green_instance_count" {
  description = "Number of instances in green environment"
  type        = number
  default     = 2
}

locals {
  traffic_dist_map = {
    blue = {
      blue  = 100
      green = 0
    }
    blue-90 = {
      blue  = 90
      green = 10
    }
    split = {
      blue  = 50
      green = 50
    }
    green-90 = {
      blue  = 10
      green = 90
    }
    green = {
      blue  = 0
      green = 100
    }
  }
}

variable "traffic_distribution" {
  description = "Levels of traffic distribution"
  type        = string
}</code></pre>

<pre><code class="bash">terraform apply -var 'traffic_distribution=blue-90'

# Verify canary's traffic.
for i in `seq 1 10`; do curl $(terraform output -raw lb_dns_name); done

# Increase traffic to green.
terraform apply -var 'traffic_distribution=split'

# Verify increased traffic.
for i in `seq 1 10`; do curl $(terraform output -raw lb_dns_name); done

# Promote green.
terraform apply -var 'traffic_distribution=green'

# Verify LB traffic.
for i in `seq 1 5`; do curl $(terraform output -raw lb_dns_name); done

# Scale down blue.
terraform apply -var 'traffic_distribution=green' -var 'enable_blue_env=false'</code></pre>

  </div>
  <div class="card-footer text-muted">
    Reference: <a href="https://learn.hashicorp.com/tutorials/terraform/blue-green-canary-tests-deployments?in=terraform/aws">Use Application Load Balancers for Blue-Green and Canary Deployments</a>
  </div>
</div>

<div class="card mb-4" id="terraform-">
  <div class="card-body">
    <h2 class="card-title"></h2>
    <p class="card-text"></p>

    <h3 class="card-title">main.tf</h3>
<pre><code class="terraform">terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 4.16"
    }
  }

  required_version = ">= 1.2.0"
}

provider "aws" {
  region  = "us-east-1"
}

module "vpc" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "3.14.0"

  name = var.vpc_name
  cidr = var.vpc_cidr  # IP address range to use
  azs = var.vpc_azs  # Availability zones.

  private_subnets = var.vpc_private_subnets

  tags = var.vpc_tags
}

resource "aws_security_group" "rds" {
  name   = "mysql_vpc_sec_group"
  vpc_id = module.vpc.vpc_id

  ingress {
    from_port   = 3306  # Port for MySQL
    to_port     = 3306
    protocol    = "tcp"
    cidr_blocks = var.allowed_public_ip
  }

  egress {
    from_port   = 3306
    to_port     = 3306
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "mysql_migration"
  }
}

resource "aws_db_parameter_group" "mysql_migration" {
  name   = "mysql-migration"
  family = "mysql8.0"  # Must correspond to RDS engine version.
}

resource "aws_db_subnet_group" "mysql_migration" {
  name       = "mysql_migration"
  subnet_ids = module.vpc.private_subnets

  tags = {
    Name = "mysql_migration"
  }
}

# MySQL instance.
resource "aws_db_instance" "mysql_migration" {
  identifier             = "mysql-migration"
  instance_class         = "db.t3.micro"
  allocated_storage      = 5
  engine                 = "mysql"
  engine_version         = "8.0.28"
  username               = "mysqlusername"
  password               = var.db_password
  db_subnet_group_name   = aws_db_subnet_group.mysql_migration.name  # Optional. If not specified, RSD is provisioned in default VPC.
  vpc_security_group_ids = [aws_security_group.rds.id]
  parameter_group_name   = aws_db_parameter_group.mysql_migration.name
  publicly_accessible    = false
  skip_final_snapshot    = true

  # Import data that already exists in S3 bucket.
  s3_import {
    source_engine         = "mysql"
    source_engine_version = "8.0.28"
    bucket_name           = "my_bucket"
    bucket_prefix         = ""
    ingestion_role        = aws_iam_role.mysql_migration.arn
  }
}

# Principle assumes an IAM role.
resource "aws_iam_role" "mysql_migration" {
  name = "mysql_migration"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Action = "sts:AssumeRole"
      Effect = "Allow"
      Sid    = ""
      Principal = {
        Service = "rds.amazonaws.com"
      }
      }
    ]
  })
}

# Define policy for migrating data from S3 bucket.
resource "aws_iam_policy" "policy" {
  name        = "mysql_migration"
  description = "Mysql policy"

  # Terraform's "jsonencode" function converts a
  # Terraform expression result to valid JSON syntax.
  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = [
          "s3:ListBucket",
          "s3:GetBucketLocation"
        ]
        Resource = "arn:aws:s3:::lane-devops-pairing-exercise-bucket"
      },
      {
        Effect = "Allow",
        Action = [
          "s3:GetObject"
        ],
        Resource = "arn:aws:s3:::lane-devops-pairing-exercise-bucket/*"
      }
    ]
  })
}

# Attach policy to IAM role.
resource "aws_iam_role_policy_attachment" "mysql_policy" {
  role       = aws_iam_role.mysql_migration.name
  policy_arn = aws_iam_policy.policy.arn
}</code></pre>

    <h3 class="card-title">variable.tf</h3>
<pre><code class="terraform">variable "vpc_name" {
  description = ""
  type        = string
  default     = "mysql_vpc"
}

variable "vpc_cidr" {
  description = ""
  type        = string
  default     = "10.0.0.0/16"
}

variable "vpc_azs" {
  description = ""
  type        = list(string)
  default     = ["us-east-1a", "us-east-1b", "us-east-1c"]
}

variable "vpc_private_subnets" {
  description = ""
  type        = list(string)
  default     = ["10.0.1.0/24", "10.0.2.0/24", "10.0.3.0/24"]
}

variable "db_password" {
  description = ""
  type        = string
  default     = "mysql"
}

variable "allowed_public_ip" {
  description = ""
  type        = list(string)
  default     = ["0.0.0.0/0"]
}

variable "vpc_tags" {
  description = ""
  type        = map(string)
  default     = { "name": "mysql_vpc" }
}
</code></pre>

    <h3 class="card-title">outputs.tf</h3>
<pre><code class="terraform"></code></pre>

  </div>
  <div class="card-footer text-muted">

  </div>
</div>
<!-- Terraform END -->

</div> <!-- /.col-md-12 -->
</div> <!-- /.row -->
</div> <!-- /.container -->

<include src="/footer.html"></include>

</body>

</html>